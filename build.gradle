/*
 * https://guides.gradle.org/creating-new-gradle-builds/
 */

plugins {
    id 'base'
    id 'eclipse'
    id 'idea'
    id 'com.palantir.git-version'
    id 'org.asciidoctor.jvm.convert'
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    new File("${rootDir}/README.adoc").withReader('utf-8') { description = it.readLine().minus('= ') }
    group = 'esy'
    version = gitVersion()
    repositories {
        mavenCentral()
    }
}

subprojects {
    // tag::setup-java[]
    plugins.withType(JavaPlugin).configureEach {
        compileJava.options.encoding = 'UTF-8'
        java.sourceCompatibility = JavaVersion.VERSION_21
        java.targetCompatibility = JavaVersion.VERSION_21
    }
    // end::setup-java[]
    tasks.withType(Test).tap {
        configureEach {
            useJUnitPlatform {
                excludeEngines 'junit-vintage'
            }
            systemProperty "file.encoding", "UTF-8"
            testLogging {
                events "passed", "skipped", "failed"
                exceptionFormat "full"
                showExceptions true
                showCauses true
                showStackTraces true
                showStandardStreams false
            }
        }
    }
}

ext.VERSION = new File("${rootDir}/VERSION").text
tasks.register('versionTag', VersionTagTask.class) {
    group = 'versioning'
}

// tag::versionCheck[]
tasks.register('versionCheck', VersionCheckTask.class) {
    group 'verification'
    allFileWithVersion.from(fileTree('.github/copilot/agents').include('*.yml'))
}
// end::versionCheck[]

// tag::versionHistory[]
tasks.register('versionHistory', VersionHistoryTask.class) {
    group = 'documentation'
    changelog = layout.buildDirectory.file("reports/HISTORY.md")
    outputs.upToDateWhen { false }
}
// end::versionHistory[]

// tag::versionRelease[]
tasks.register('versionRelease', VersionReleaseTask.class) {
    group = 'documentation'
    changelog = layout.buildDirectory.file("reports/RELEASE.md")
    outputs.upToDateWhen { false }
}
// end::versionRelease[]

// tag::buildChangelog[]
tasks.register('buildChangelog', BuildChangelogTask.class) {
    group = 'build'
    changelog = layout.buildDirectory.file("reports/CHANGELOG.md")
    outputs.upToDateWhen { false }
}
// end::buildChangelog[]

import org.asciidoctor.gradle.jvm.AsciidoctorTask
tasks.register('pages', AsciidoctorTask) {
    group = 'documentation'
    description = 'Builds api documentation'
    sourceDir = file('doc')
    sources { 
        include '**/*.adoc'
        exclude '**/_*.adoc'
        exclude '**/template'
    }
    outputDir = file('pages/html/server')
    baseDir = file('.') // == includedir
    attributes = [
        'includedir': rootProject.projectDir.absolutePath.replace('\\', '/'),
        'source-highlighter': 'coderay',
        'toc': 'left',
        'toclevels': 4,
        'sectanchors': true,
        'sectlinks': true,
        'linkattrs': true,
        'allow-uri-read': true,
        'icons': 'font'             
    ]
}

tasks.named("asciidoctor") {
  enabled = false
}
