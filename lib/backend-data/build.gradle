plugins {
    id 'java-library'
    id 'eclipse'
    id 'jacoco'
    id 'io.spring.dependency-management'
    id 'org.springframework.boot'
}

dependencies {
    api project(':lib:backend-api')
    // https://projectlombok.org
    implementation('org.projectlombok:lombok')
    annotationProcessor('org.projectlombok:lombok')
    // https://spring.io/projects/spring-boot
    api('org.springframework.boot:spring-boot-starter-tomcat')
    api('org.springframework.boot:spring-boot-starter-jackson')
    api('org.springframework.boot:spring-boot-starter-webmvc')
    api('org.springframework.boot:spring-boot-starter-actuator')
    api('org.springframework.boot:spring-boot-starter-security')
    api('org.springframework.boot:spring-boot-starter-validation')
    api('org.springframework.boot:spring-boot-starter-liquibase')
    // https://spring.io/projects/spring-data
    api('org.springframework.boot:spring-boot-starter-data-rest')
    api('org.springframework.boot:spring-boot-starter-data-jpa')
    api('org.springframework.data:spring-data-rest-hal-explorer')
    // https://spring.io/projects/spring-graphql
    api('org.springframework.boot:spring-boot-starter-graphql')
    api('io.micrometer:micrometer-registry-prometheus')
    // https://liquibase.org/
    api('org.liquibase:liquibase-core')
    // http://hsqldb.org/
    runtimeOnly('org.hsqldb:hsqldb')
}
dependencies {
    // https://spring.io/projects/spring-boot
    testImplementation('org.springframework.boot:spring-boot-starter-jackson-test')
    testImplementation('org.springframework.boot:spring-boot-starter-webmvc-test')
    testImplementation('org.springframework.boot:spring-boot-starter-actuator-test')
    testImplementation('org.springframework.boot:spring-boot-starter-security-test')
    testImplementation('org.springframework.boot:spring-boot-starter-validation-test')
    testImplementation('org.springframework.boot:spring-boot-starter-liquibase-test')
    // https://spring.io/projects/spring-data
    testImplementation('org.springframework.boot:spring-boot-starter-data-rest-test')
    testImplementation('org.springframework.boot:spring-boot-starter-data-jpa-test')
    // https://spring.io/projects/spring-graphql
    testImplementation('org.springframework.boot:spring-boot-starter-graphql-test')
    // https://spring.io/projects/spring-restdocs
    testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')
    // https://junit.org/junit5
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
    // https://site.mockito.org/
    testImplementation('org.mockito:mockito-junit-jupiter')
}

test {
    filter {
        failOnNoMatchingTests = false
    }
    reports {
        html.required = true
        html.outputLocation = rootProject.layout.projectDirectory.dir("pages/html/" + project.name + "/junit5")
    }
    jacoco {
        enabled = true
    }
}

jacocoTestReport {
    mustRunAfter test
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: ['**/Q*.class'])
    }))
    reports {
        csv.required = false
        html.required = true
        html.outputLocation = rootProject.layout.projectDirectory.dir("pages/html/" + project.name + "/jacoco")
        xml.required = true
    }
}

jar {
    enabled = true
    manifest {
        attributes 'Specification-Title': project.name
        attributes 'Specification-Version': VERSION
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
    }
}

bootJar {
    enabled = false
}

bootRun {
    enabled = false
}

bootBuildImage {
    enabled = false
}
