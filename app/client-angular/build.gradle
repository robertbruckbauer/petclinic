plugins {
    id 'base'
    id 'com.github.node-gradle.node'
}

clean {
    delete ".vite"
}

assemble {
    dependsOn 'npmInstall'
}

tasks.named("npmInstall") {
    // No node in CI-Job build-java
    onlyIf { System.env['CI'] == null }
}

build {
    dependsOn 'npmBuild'
}

tasks.register('npmBuild', NpmTask) {
    // No node in CI-Job build-java
    onlyIf { System.env['CI'] == null }
    mustRunAfter 'npmInstall'
    // https://docs.npmjs.com/cli/v10/commands/npm-run-script
    npmCommand = ['run', 'build']
}

check {
    dependsOn 'npmCheck'
}

tasks.register('npmCheck', NpmTask) {
    // No node in CI-Job build-java
    onlyIf { System.env['CI'] == null }
    mustRunAfter 'npmInstall'
    // https://docs.npmjs.com/cli/v10/commands/npm-run-script
    npmCommand = ['run', 'test']
}

import java.util.Optional;
ext.CLIENT_NAME = Optional.ofNullable(System.getenv("CLIENT_NAME"))
        .orElse(rootProject.name + "-" + project.name)
ext.CLIENT_IMAGE = Optional.ofNullable(System.getenv("CLIENT_IMAGE"))
            .orElse(rootProject.name + "/" + project.name)
ext.CLIENT_TAG = Optional.ofNullable(System.getenv("CLIENT_TAG"))
            .orElse('latest')

// https://docs.docker.com/reference/cli/docker/build/
// https://docs.docker.com/reference/cli/docker/image/tag/
tasks.register('buildImage') {
    group = 'build'
    mustRunAfter 'build'
    enabled = true
    doLast {
        String image1 = CLIENT_IMAGE + ":" + CLIENT_TAG;
        String image2 = CLIENT_IMAGE + ":" + VERSION;
        println 'Building image ' + image1
        providers.exec {
            workingDir(projectDir)
            executable 'docker'
            args 'build', '.', '-t', image1
        }.result.get()
        if (image1 != image2) {
            providers.exec {
                workingDir(projectDir)
                executable 'docker'
                args 'tag', image1, image2
            }.result.get()
            println 'Built image to docker daemon as ' + image1 + ", " + image2
        } else {
            println 'Built image to docker daemon as ' + image1
        }
    }
}

tasks.register('versionCheck', VersionCheckTask.class)  {
    group = 'verification'
    mustRunAfter 'npmInstall'
    allFileWithVersion.from("package.json", "package-lock.json")
}

tasks.register('lint', NpmTask) {
    group = 'verification'
    // No node in CI-Job build-java
    onlyIf { System.env['CI'] == null }
    mustRunAfter 'npmInstall'
    npmCommand = ['run', 'prettierCheck']
}

tasks.register('format', NpmTask) {
    group = 'build'
    // No node in CI-Job build-java
    onlyIf { System.env['CI'] == null }
    mustRunAfter 'npmInstall'
    npmCommand = ['run', 'prettierApply']
}
